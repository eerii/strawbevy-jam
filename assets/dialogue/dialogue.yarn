title: Start
tags: 
position: -583,-20
---
// Variables
<<declare $anxiety = 0 as number>>
<<declare $offense = 0 as number>>
<<declare $nonsense = 0 as number>>
<<declare $alcohol = 0 as number>>
<<declare $busy_work = false as bool>>
<<declare $talk_about_nico = false as bool>>
<<declare $deadname = false as bool>>
<<declare $weather = "" as string>>
<<declare $drink = "" as string>>
// Start
Remie: Hey! I-it's been a while.
Player: Yeah, thanks for coming on ___.
-> (this) rainy day // We are going to use parentheses to indicate parts that can change between questions
  <<set $weather to "rainy">>
-> (this) sunny day
  <<set $weather to "sunny">> 
// No need for other
Remie: Oh please, thank [happy]you[/happy]. I hope hearing from me wasn't a bother. 
Player: Not at all! I was really excited to see you again.
Player: Hmm... it seems like they're having ___.
-> (a) busy day
  <<set $busy_work to true>>
  <<set $anxiety to $anxiety + 1>>
  Remie: Yeah, we weren't the only ones to think to have a drink today.
  <<jump BusyDay>>
-> (a) calm day
  Remie: Yeah, thank goodness! This place can get really [anxious]crowded[/anxious] sometimes.
  <<jump WaiterComes>>
-> (a) rainy day // Same as a busy day
  <<set $busy_work to true>>
  <<set $anxiety to $anxiety + 1>>
  Remie: I mean, that's one way to put it, right?
  <<jump BusyDay>>
-> (a) sunny day // Same as a calm day
  Remie: Yeah, it feels so [happy]calm[/happy] in here, specially with all the rain pouring outside.
  <<jump WaiterComes>>
// No need for other
===

title: BusyDay
position: -318,-247
---
Remie: Marco's going to take a while...
Player: Wait, is Marco still the waiter?
Remie: Of course he is! He's the [proud]best[/proud], can't get fired. 
Player: Haha I suppose that's true.
Remie: Seems like you don't come round here often. 
Player: Well, not really. It's not ___.
-> (a) good momment | (a) sunny day // We can use |Â to say that two options lead to exactly the same result
  Remie: Oh, did something happen?
  Player: [anxious]I...[/anxious] I just don't wanna talk about that right now, sorry. 
  Remie: Don't be sorry. Is there something I can do to make you feel [happy]confortable[/happy]?
  Player: Don't worry, it's fine. I'm fine. 
  Remie: Ok...
  Remie: You know what? This table made me remember something that always cheers me up.
  Remie: I still have the [proud]picture[/proud]
  <<set $talk_about_nico to true>>
-> (the) old days
  <<set $anxiety to $anxiety + 1>>
  Remie: [anxious]W-well[/anxious], I thougth you liked the old days.
  Player: I used to, yeah... but they're in the past. 
  Remie: And you don't want to remember the past?
  Player: Is that why you invited me here? To dwell on ancient times? 
  Remie: Ahm... not really...
  Remie: Do you remember the picture?
-> other
  <<set $nonsense to $nonsense + 1>>
  Remie: I don't think I quite get what you mean.
  <<wait 1>>
  Remie: You know what I just thought, sitting on this table?
  Player: Hmm... How much they need a renovation?
  Remie: No silly, I remembered [happy]the picture[/happy]
<<jump CrookedPicture>>
===

title: WaiterComes
position: -362,151
---
<<marcoComes>>
Waiter: Hey guys, having ya both back is great!
Waiter: Remie I'm used to seeing, but Nico, long time no see. Ya gotta catch me up later.
Remie: Hey, Marco! [happy]You[/happy] need to tell me all about this new person you're seeing.
Waiter: Oh, just ya wait, you're in for a treat. But first, can I get ya something to drink?
Remie: Nico, are you having the same as always?
Player: I guess I'll have ___
-> (an) orange juice
  Remie: An orange juice? That's new.
  <<set $drink to "orangejuice">>
  <<jump OrangeJuice>>
-> (a) beer
  Remie: Back to the old days...
  <<set $alcohol to 1>>
  <<set $drink to "beer">>
-> (a) good moment
  Remie: So that's a beer, am I right?
  Player: You know me too well.
  <<set $alcohol to 1>>
  <<set $drink to "beer">>
-> (a) rainy day
  Remie: Well, I guess it's a beer then.
  <<set $alcohol to 1>>
  <<set $drink to "beer">>
-> other
  <<set $nonsense to $nonsense + 1>>
  Remie: You always have the weirdest drinks.
  <<set $drink to "weird">>
<<jump OtherDrink>>
===

title: OrangeJuice
position: -95,65
---
Remie: I'm having the same! I recently developed an obsession with [happy]orange juice[/happy]. 
Waiter: Two orange juices right up! I'm defenitly sitting with both of ya as soon as I can. 
Player: Please do!
<<marcoLeaves drinks>>
<<wait 1>> // Wait for a bit
Player: So... An obsession with orange juice? 
Remie: Yep! Somehow this drink always puts me at ease. 
Player: Oh, what ___!
-> (a) weirdo
  <<set $anxiety to $anxiety + 1>>
  <<set $offense to $offense + 1>>
  Remie: Jeez... no need to be so harsh.
-> (a) coincidence
  <<set $anxiety to $anxiety - 1>>
  Remie: It's such a tasty drink, everyone is missing out.
-> other
  <<set $nonsense to $nonsense + 1>>
  Remie: You're not making much sense, Nico.
  Player: Sorry, I'm a bit out of it today.
  Remie: Don't apologize, please. Is there something I can do to make you feel more [happy]comfortable[/happy]?
  Player: It's nothing. I'm [anxious]fine[/anxious].
  <<set $talk_about_nico to true>>
  Remie: Ok...
  Player: It's cool. The orange juice I mean.
Player: If you like it so much you should get a poster of it.
Remmie: I would if I had some free space in my room.
Remmie: I still have the picture, by the way.
<<jump CrookedPicture>>
===

title: OtherDrink
position: -71,228
---
Remie: I'm having an orange juice Marco, thanks.
<<if $drink == "beer">>
  Waiter: So, an orange juice, a beer, and some tea I gotta spill.
<<else>>
  Waiter: So, an orange juice, an... imma bring you a glass of water, and some tea I gotta spill. 
<<endif>>
Waiter: Be right back!
<<marcoLeaves drinks>>
<<wait 1>>
Remie: So, we really are on the same bar as two years ago. 
Player: Well, we're even on the same table.
Remie: [happy]Of course![/happy] I love this table. Do you remember our game nights? 
Player: Yes I do! And ___ 
-> (the) hour long arguments
  <<set $anxiety to $anxiety + 1>>
  Remie: Those... I guess we can get really stubobrn.
-> (the) band performances
  Remie: Shut up! Those really were so funny.
-> (the) good moment (s)
  Remie: Yeah, we got plenty of those, didn't we?
  <<set $anxiety to $anxiety - 1>>
-> (the) busy day (s) | (the) calm day (s) | (the) rainy day (s) | (the) sunny day (s)
  Remie: Those were the days, huh?
-> other
  <<set $nonsense to $nonsense + 1>>
  Remie: I'm having a hard time remembering that.
Player: It seems like ___ ago
-> (a) long time
  <<set $anxiety to $anxiety + 1>>
  Remie: Does it? For me is an everyday thing, after all, I see the picture every morning.
-> (a) little while
  <<set $anxiety to $anxiety - 1>>
  Remie: Does it? For me is an everyday thing, after all, I see the picture every morning.
-> hour long arguments
  <<set $anxiety to $anxiety + 1>>
  Remie: I guess you can measure time in arguments. We didn't have a shortage of them.
  Remie: Every time I look at the picture I can't help but remembering everything.
-> other
  <<set $nonsense to $nonsense + 1>>
  Remie: You are always so weird.
  Remie: Like in the picture.
<<jump CrookedPicture>>
===

title: CrookedPicture
position: 181,-4
---
Player: Wait, the crooked picture?
<<discard>> // Make the player discard their cards when entering a new scene
Remie: Yep, we look so goofy.
<<if $talk_about_nico>>
    <<jump TalkAboutNico>>
<<else>>
    <<jump TalkAboutRemie>>
<<endif>>
===

title: TalkAboutNico
position: 600,-57
---
Player: You keept it...
Player: That's kind of ___ to me.
-> (an) important thing
  <<set $anxiety to $anxiety - 1>>
  Remie: I am so relieved you said that.
  Remie: Sometimes when I think about my life before coming out it feels unreal.
  Remie: Like I was watching a movie character, not myself.
  Remie: This picture... it reminds me that there can be light in unexpected places.
  Remie: Even if it's crooked haha.
  Remie: I hope it can do the same for you.
-> (a) trigger
  <<set $anxiety to $anxiety + 1>>
  Remie: Oh...
  Remie: Can I ask you why?
<<jump NicoDepressed>>
===

title: NicoDepressed
position: 960,-50
---
Player: You know?
Player: In these two years I achieved ___ I wanted.
-> everything | (every) important thing
  Remie: Oh, did you finally get your doctorate?
  Player: Yes.
  Remie: Congrats! It must have been a lot of work.
  Remie: What about Marie? And your son?
  Player: They couldn't be better.
-> nothing
  <<set $anxiety to $anxiety - 1>>
  Player: I never feelt so bad, and it gets worse every day.
  Remie: Nico... did something happen with Marie?
  Player: No, we are still together. 
  Remie: Academic troubles?
  Player: Not really. 
  Player: I'm a doctor now.
  Remie: Well, congrats! You finally got it.
-> (any) trigger
  <<set $nonsense to $nonsense + 1>>
  Remie: What do you mean?
  Player: I don't know where my head is today.
  Player: But to be honest, I don't know where it has been lately.
  Remie: Nico, are you ok?
  Player: I should be. I have a loving family, I got my doctorate...
  Remie: You did? Congratulations!
Remie: Shouldn't you be happy, then? 
<<jump NicoPerfect>>
===

title: NicoPerfect
position: 1248,-48
---
Player: Well, I thought so.
Player: I thought that if I got to the finish line, everything would be fine.
Player: But every time I got closer, there was something else.
Remie: Like when you got that paper published, and we got together to celebrate.
Player: Oh... you remember that time.
Remie: Of course. We were all so happy, but you didn't really enjoy it.
Remie: And then you spent the next two weeks in your office, working on your thesis.
Player: Yeah...
Player: The things that are supposed to make me happy are just tiny speckles.
Player: But I really thought that after I finish them all, I could finally be happy.
Remie: You can't really treat life like a to-do list.
Player: I know. Or at least I thought I knew.
Player: Now that I have all I wanted, it feels like ___.
-> (a) waste of time | nothing | (not) (an) important thing
-> (a) jail
  Remie: I think I can relate with that haha.
-> everything
  Remie: Everything?
  Player: Yeah, like even everything is not enough.
-> (pulling) (the) trigger
  <<set $anxiety to $anxiety + 2>>
  Remie: Nico, are you ok?
  Player: I'm fine. I didn't mean to say that.
<<wait 1>>
Player: I may be crooked too [anxious]haha[/anxious].
Remie: Well, that makes two of us.
Player: It's not like I don't love my wife anymore.
Player: Or that suddenly I hate mathematics.
<<if $anxiety < 4>>
  Remie: I would end you if you did, after all the hours I had to listen you rumble about...
  Remie: What was it called, again? Motivations theory?
  Player: Haha, yeah. I may have talked too much about [proud]motives[/proud] theory.
  Remie: That's the one!
  Remie: Also sorry, hope you know I was just joking.
  Remie: I liked listening to you talk about it, even if I didn't understand a thing.
  Remie: Anyways, sorry for getting sidetracked.
  Player: Don't apologize. It's just that, nothing is [anxiety]terrible[/anxiety]...
<<endif>>
Player: But I don't do sports anymore.
Player: I don't see my friends.
Player: And the motivation that I had when I was studying,
Player: now I got what I wanted,
Player: is no more.
Remie: I think I get that.
Remie: Like everything is fine with your life but there's someone on the backseat that makes you unable to enjoy it.
Player: ___.
-> !Exactly
  <<set $anxiety to $anxiety - 1>>
  <<jump Exactly>>
-> !Not really
  <<jump NotReally>>
===

title: Exactly
position: 1620,-53
---
<<set $anxiety to $anxiety - 1>>
Remie: I felt like that before I knew I was enby.
Remie: Of course I'm not trying to say you are in the middle of a [scary]gender crisis[/scary].
Remie: But I know the feeling of not being able to recognize yourself in your life.
Remie: You shouldn't have guilt, it's not as if you're doing anything bad. 
Remie: Sometimes things just don't fit in together.
Player: I suppose it makes sense. 
Player: Thanks.
Player: It's just... my life is like ___.
-> nothing | (a) waste time | (a) jail | (not) (an) important thing
  <<set $anxiety to $anxiety - 1>>
  Player: Sometimes I don't really feel like living it. 
  Remie: Hey, please, don't say that. 
  Remie: I know it can be overwhelming sometimes. 
  Remie: And you have a lot of pressure on you.
  Remie: You did what you desired the most.
  Remie: But that doesn't mean that you [intensely]have to[/intensely] to be right. 
  Remie: Everyone needs help sometimes, that includes you. 
  Remie: All of us have to deal with the conflict from time to time. 
-> everything
  Player: Everything to me. 
  Player: And the mere idea of not enjoying it is disgraceful. 
  Remie: I know it can be overwhelming sometimes.
  Remie: And I know that you may be thinking that the hole you feel you have in your life is wrong.
  Remie: But it's not that.
  Remie: Not everything has to always fit together.
  Remie: It doesn't mean that you have to get away from all of it.
  Remie: Perhaps you may need a different approach.
  Player: Well, I got away you and now everything is worse. 
  Remie: Exactly, kicking me out is the most [proudly]terrible idea[/proudly] anyone could have.
  Remie: You can have to ask my parents about it.
  Player: Wait, wha...?
-> (grabbing) (the) trigger
  <<set $anxiety to $anxiety - 1>>
  Player: Ready to give up.
  Remie: Nico, please, don't say that.
  Remie: I know it can be overwhelming sometimes.
  Remie: When you are drowning, you can't think about anything else.
  Remie: But please, don't give up.
  Remie: You might think that it will never get better, but it will.
  Remie: I know it's hard to believe, but it will.
  Player: I don't know.
  Remie: You don't need to. You can figure it out on the way.
  Remie: You can have a lot of pieces that don't fit together and still be happy.
  Player: Thanks. It feels good to talk to you.
  Remie: I really missed this, you know?
<<jump AfterNicoGood>>
===

title: NotReally
position: 1441,-307
---
<<set $anxiety to $anxiety + 1>>
Remie: So, what is it then? 
Remie: You know, what you're telling me feels like my life before knowing I'm enby.
Remie: This uncunny lack of selfrecognition.
Remie: I think it is completly normal, even for a cis person.
Player: Well, I don't think we are the same.
Player: You are ___ and I'm...
-> (a) waste of time | nothing
  <<set $anxiety to $anxiety + 2>>
  Player: I'm just here because of pity.
  Player: I thought you may have changed a bit.
  Player: That perhaps we could have a nice chat, and that would be it.
  Player: But I didn't really want to come.
  Player: This recognition you talk about is bullshit.
  Player: Please don't compare me with... whatever you are.
  Player: I haven't even thought of you these past two years.
  <<jump AfterNicoBad>>
-> (a) jail | (a) trigger
  <<set $anxiety to $anxiety + 2>>
  Player: I just can't.
  Player: Nothing feels right with you.
  Player: Being here reminds me why we are not friends anymore. 
  Player: My life has nothing to do with this nonsense you talk about.
  <<jump AfterNicoBad>>
-> everything
  Player: I'm just so envious of you.
  Player: In a good way. Well. I hope so.
  Player: I didn't really understood when you... you know.
  Player: And I'm so sorry for that.
  Player: We may not be the same, but what you say makes a lot of sense.
  Player: I really admire you, you know?
  Player: You know how to be ok with your life without depending on some arbitrary goals.
  Player: You have the endurance to take actions in virtue of what it's good for you, regardless of some made-up end.
  <<jump AfterNicoGood>>
-> (an) important thing
  Player: I'm silly. Uncapable to recognize it.
  Player: I couldn't have expected to solve all my problems by seeing you today. 
  Player: Or that you would accept me like that.
  Player: Specially after I dissapeared from your life so suddenly.
  Player: If I'm being honest, I don't really understand you yet.
  Player: No, we are not the same: I found you so different that I couldn't even kept a relationship with you.
  Player: But with time I have learnt that distance can be important, an so are you.
  <<jump AfterNicoGood>>
===

title: AfterNicoGood
position: 1787,-386
---
<<discard>>
Remie: Oh, look who's coming.
<<marcoComes>>
<<if $busy_work>>
  Player: Marco! I can't belive you still work here. 
  Waiter: What can I say, I'm simply the best.
  Waiter: I'm surprised to see ya coming back, Nico. And along Remie!
  Player: Yeah, I didn't come in a while.
  Waiter: Only true if you call a while two years.
  Waiter: Remie, I need help, we really gotta explain maths to this lad. 
  Remie: Oh absolutely.
<<endif>>
Remie: Do you have some time to sit with us for a bit, Marco?
Waiter: I finally do! But don't ya want a drink?
Player: We can have it later!
Waiter: What have you been doing this past years, Nico? 
Player: Well, I spent most of the time ___.
-> (at) work
  <<jump NicoWork>>
-> (with) (my) family
  <<jump NicoFamily>>
===

title: NicoWork
position: 2125,-220
---
Waiter: Ah yes, that [bored]mathematician[bored] stuff.
Remie: Marco, careful, he might divide us in half haha.
Player: Thanks, lads, I missed you being sooo supportive. 
Waiter: Sure ya did! So, still one of those doctoral students? 
Player: Well, not really.
Player: I finished my thesis. 
Waiter: Wow! That's great, congrats, Mr. Doctor! I don't know what are ya drinking but it's on the house. 
Waiter: You must be living a really sweet life at the momment.
Player: Well, kinda.
Waiter: Don't seem very sure.
Player: I was about to tell Remie in this update date...
Player: But now I'm ___.
-> uneployed | (out) (of) work
  Remie: What?
  Waiter: Really?
  Remie: Yeah, are there any other motives theory specialists, like, in the world? 
  Player: Of course there are, it is a very serious line of work! 
  Player: I was an intern for a while. 
  Player: But then depression kicked in, I don't really know why. 
  Player: So some days I wasn't, like, going to work.
  Player: The only reason I got up of bed was to drive Dani to school.
  Player: When the intership was due renewal, they weren't very subtle about not wanting me there anymore.
  Player: Honestly, I can't be mad at them.
  Player: I guess I'll look at an industry job or something.
-> (an) intern
  Waiter: But that's fantastic, isn't it?
  Player: Well, I have two sons,
  Player: a mortage,
  Player: many bills,
  Player: and they are paying me peanuts.
  Player: Peanuts might even be generous.
  Player: I'm considering quitting.
  Player: Maybe try to find a job in the industry, they really want mathematicians.
Remie: Wait, [firmly]no[/firmly]! You've being dreaming of working in academia for as long as I know you. 
Player: I did, yeah, but now I have responsabilities.
Remie: Yeah, you have, but your responsabilities need someone to rely on.
Remie: And you can't be that person if you are not ok.
Remie: Look, I had to change careers. I'm a carpenter now. 
Remie: Yeah, unexpected, I know.
Waiter: They even made some of the bar's chairs, kinda good.
Remie: Only kinda? I'm the best, honey.
Remie: And, you know, carpentry is not known for being the most open minded job there is.
Remie: There are a lot of... well, guys. And I struggled because of my gender.
Remie: But I was desperate and I really enjoy the manual work, so I fought to be here.
Remie: I ended up with a very good team who madly supports me.
Remie: That's everything I could want.
Player: So they're like your ___.
-> Viena's circle
  Remie: Is Viena's circle the math freak's name for trusty people?
  Player: Well, it seems like Grothendieck was there at some point. 
  Player: So yes, it is. 
  Remie: I don't understand a word of what you're saying
-> therapist
  Remie: No, not all.
  Remie: I have a therapist as my therapist, silly.
  Remie: They are my support system.
-> family
  Remie: You can bet they are.
  Remie: I don't really belive in the whole chosen family thing.
  Remie: It's not like I chose them.
  Remie: But they hold me.
  Remie: And I love them for it.
-> other
  Remie: Again with the nosenses.
  Remie: Are you ok?
  Remie: I know we didn't see each other in a while, but I care about you. 
  Player: I'm... [emotional]thanks[/emotional].
  Player: Just a bit drained, that's all.
  Remie: We can talk about something else if you need to. 
  Player: No, please. This is really helping me.
  Remie: I'm glad it does.
Remie: Look, what I'm trying to say is I know it can be hard.
Remie: And honestly, having to work is so shitty.
Remie: Is very hard to enjoy it even a litle bit.
Remie: But that's precisely why you should keep trying to be somewhat comfortable with what you do. 
Remie: Because having something or someone to fight for is really worth it.
Remie: And that's my presidental speech, thank's for coming.
Waiter: I'll vote for you!
Player: Haha guess I needed it.
Player: Thanks for today.
Player: We really should do it again.
<<theEnd 1>>
===

title: NicoFamily
position: 2116,-392
---
Waiter: Oh how's little Dani?
Player: He's great, doing elementary school now.
Player: Also in love with his new ___.
<<declare $sibling = "" as string>>
-> brother
  <<set $sibling to "brother">> 
-> sister
  <<set $sibling to "sister">>
Waiter: Wait, another one?
Remie: Really, you are educating them in binarism already? 
Waiter: Haha.
Player: Yes, we are. One thing at a time, please.
<<if $sibling == "brother">>
  Waiter: What's his name?
  Player: Alexander. 
  Player: After Grothendieck.
  Remie: Wait, did tou really called your son after your favourite mathematician? 
  <<wait 1>>
  Player: Yes.
  Waiter: Ahaha some things never change. 
  Waiter: It's a beautiful name, by the way.
<<else>>
  Waiter: What's her name? 
  Player: Kora. 
  Remie: Well, I can forgive you for the binarism. It's a beautiful name. 
  Player: It was Sophie's idea. 
  Remie: She is definetly the smart one.
<<endif>>
Waiter: But, wait. 
Waiter: Isn't this supposed to be good? 
Player: Well, yes. I'm so happy to have them of course.
Player: But things between Sophie and I are not going too well lately.
Player: Its nothing really, but the monotony and the responsabilities are taking us down. 
Remie: Are you with her and the kids, like, at all? 
Player: Yeah, as much as I can. But, you know, work's hard, I don't have much time.
Remie: I know you don't and I'm sure they know it too.
Remie: But knowing sometimes is not enough.
Remie: You know, my parents and I don't talk anymore.
Remie: No since I came out.
Remie: And it's not just because they're transphobes, but also because we didn't really have any kind of relationship before.
Remie: It's ok not having parents, there's a life beyond family. 
Remie: But it's also hard.
Remie: I'm not saying you are my parents. However...
Remie: Living your family and social relationships like "this things I do while I'm not working" is terrible.
Remie: So maybe what you're missing is just an honest relation.
Remie: Well, I hope I'm not upsetting you too much.
Player: No, please, thanks for your honesty.
Player: Sometimes I can be very stubborn.
Waiter: Sure you can.
Player: [ironic]Thanks[/ironic], Marco.
Player: I whished you could have told me that before. But that's also my fault.
Remie: Nico admitting to a mistake, that is a historical momment. 
Waiter: Ya bet it is.
Player: Thanks for your jokes, also please stop ahaha.
Remie: I'm sorry, I'm sorry.
Remie: I really missed this.
Waiter: Well, someone needs to keep coming to this place to keep me employed.
Waiter: Maybe ya could make a habit of it again.
Waiter: Only saying that with a business mindset, it has nothing to do with me linking seeing you.
Player: If you say so ahaha.
Player: But we should do it again.
<<theEnd 3>>
===

title: AfterNicoBad
position: 2126,-54
---
<<discard>>
<<marcoComes>>
Waiter: Hey, lads, it's great to have ya both ba...
Remie: You know what?
Remie: I don't have to endure this.
Remie: And I'm not doing it.
Remie: Thakfuly I have a good job and amazing group of people by my side.
Remie: I didn't even want for us to be friends again. 
Remie: I just needed to check on you, to check on my past.
Remie: But I guess you haven't grown a bit.
Remie: I'm enby! For goodness sake.
Remie: Whether you like it or not.
Remie: I don't need you to afirm me.
Remie: If you can't really respect that about myself there is no point in us talking.
Remie: Goodbye.
<<remieLeaves>>
<<wait 1>>
Waiter: What's happening here?
Player: Oh, hey Marco.
Player: It's nothing. 
Player: You know he can be ___
-> a drama queen
-> an idiot
Waiter: No, I don't know that.
Waiter: They have been a very good friend, someone you can relay on that makes me enjoy our time together.
Waiter: I can't say the same about you.
Waiter: You have the balls to dissapear for two fucking years, and now you come here and misgender my friend?
Waiter: You know English, you can say "they".
Player: Oh come on.
Player: He just wanted me to come here to show off in front of me, it was just revenge.
Waiter: You know what? I don't want you here.
Waiter: Get out of the bar.
Waiter: I don't wanna see you again.
<<theEnd 2>>
===

title: TalkAboutRemie
position: 610,127
---
Player: So you still have it ___.
-> (in) the same place
  <<set $anxiety to $anxiety + 1>>
  Remie: Not really. 
  <<wait 1>>
  Remie: My parents kicked me out of home when I came out.
  Remie: I'm sharing an apartment downtown.
  Remie: The picture is one of the few things I got from my house after... well, [anxious]that[/anxious].
  Remie: But hey, it's still crooked.
  Player: I'm sorry. Your parents are such jerks.
  Remie: Well, it is what it is.
  Remie: No point beating a dead horse. 
  <<if $busy_work>>
    <<jump BusyDayMarcoDrinks>>
  <<else>>
    <<jump AfterMarcoComes>>
  <<endif>>
-> (after) all that happened
  Remie: Did you want me to throw it away?
  <<wait 1>>
  Remie: Nevermind. Don't answer that.
  Remie: I was close, and I was tempted to leave it behind when I moved out.
  Remie: Now I have this nice job, and a [proud]supportive[/proud] group of friends.
  Remie: In this time when we didn't see each other I made myself a life, one worth living.
  Remie: That picture reminds me:
  Remie: "You can look at the past".
  Remie: That's why I invited you here today.
  <<jump LookDeadname>>
===

title: LookDeadname
position: 721,367
---
Player: Look, ___, about that...
-> !Remie // ! is used to specify that only these options should be available
  Remie: I know.
  Remie: I don't expect everything to be the same as before.
  Remie: I know you're not the same person you were when we were friends.
  Remie: I certainly am not.
  Remie: But I'm glad you came.
  Player: I'm glad too.
-> !-redacted-
  <<set $anxiety to $anxiety + 2>>
  <<set $offense to $offense + 2>>
  <<set $deadname to true>>
<<if $busy_work>>
  <<jump BusyDayMarcoDrinks>>
<<elseif $deadname>>
  <<jump AfterDeadname>>
<<else>>
  <<jump AfterMarcoComes>>
<<endif>>
===

title: BusyDayMarcoDrinks
position: 944,126
---
<<marcoComes>>
Waiter: Hey guys! I didn't think I would see ya both together again!
<<if $deadname>>
  Waiter: Sorry for not coming ear...
  Remie: Don't worry, Marco. Can you please leave us for another moment?
  Waiter: Of course, is everything ok?
  Remie: Yes, I just need to talk with my [sarcastic]friend[/sarcastic].
  Waiter: Sure, I'll be back in a few.
  Waiter: Call me if you need... anything.
  <<marcoLeaves>>
  <<wait 1>>
  <<jump AfterDeadname>>
<<else>>
  Waiter: I whish I had less work so I could chat with ya for a few.
  Remie: Don't worry, Marco! You can join [emotional]us[/emotional] later.
  Waiter: For now, what can I get ya?
  Remie: I'm having an orange juice please.
  Player: I'll have ___.
  -> (the) same
    <<set $drink to "orangejuice">>
    Remie: You also like orange juice?
    Player: It's not my favorite, but I don't want to drink alcohol.
  -> (a) beer
    <<set $drink to "beer">>
    <<set $alcohol to 1>>
    Remie: It seems like the picture is not the only thing that stays the same.
  Waiter: Will be with ya as soon as I can.
  Player: It's really nice to see you, Marco.
  <<marcoLeaves drinks>>
  <<wait 1>>
  <<jump AfterMarcoComes>>
<<endif>>
===

title: AfterDeadname
position: 1368,365
---
Remie: You know what, Nico?
Remie: I really thought we could talk again, but I guess I was wrong.
Player: I didn't mean to ___.
-> offend (you)
  <<set $anxiety to $anxiety - 2>>
  Player: It's just a name.
  Remie: You know is more than that. 
  Remie: Calling someone by their deadname [emphasis]is[/emphasis] offensive, no matter the intention. 
  Remie: Why do you think we didn't see each other in two years? 
  Remie: You cant't even treat me with the bare minimum of respect.
-> (give) (you) hope
  <<set $anxiety to $anxiety + 2>>
  Player: I know I shouldn't have accepted your invitation.
  Remie: Give me hope? Hope for what?
  Remie: Look, I did not came here to desperatly beg be your friend again.
  Remie: I just needed to make sure I could deal with the past. 
  Remie: In a more phisical way than just a picture. 
  Remie: My life is better without you.
Player: You know? You're __.
-> right
  Player: Yes, I could have been better to you. 
  Player: But I don't really know how.
  <<if $anxiety > 5>>
    Player: I'm sure I did many things poorly.
    Player: But I can't understand how to do them better.
    Remie: I don't think it is that difficult.
    Remie: Like, to start with, you should call me by my name.
    Remie: It's a bare minimum, Nico, for goodness sake.
    Remie: Are you really waiting for me to explain that? 
    Player: Yeah, isn't that why we are here?
  <<else>>
    Player: And that's not even trying to be an excuse.
    Player: I'm a mess, and I can't even bring myself to treat you with respect.
    Remie: If you are looking for pity...
    Player: No. I'm done doing that.
    <<jump NeutralEnding>>
  <<endif>>
-> wrong
  Player: You can say that you don't need this, but you are be wrong.
  <<set $anxiety to $anxiety + 1>>
  Player: Of course you need it.
  Player: That's why you set up all of this.
  Player: I just came here out of pity.
-> offend (ing)
  Player: Offending me.
  <<set $anxiety to $anxiety + 1>>
  Remie: I am what?
  Player: Yes, you are.
  Player: Do you really think you can come here and say that I'm nothing?
  Player: I'm sorry, lad, but it's clear I'm something to you.
  Player: You wouldn't have called me here otherwise. 
-> hope (less)
  Player: For me.
  Player: And I suppose for so many other people too.
  <<set $anxiety to $anxiety + 1>>
  Player: I don't really know why I'm still here.
-> (in) the same place
  Player: Just in the same place as two years ago. 
  <<set $anxiety to $anxiety + 1>>
  Player: You can fool yourself thinking things have changed, but it's just a lie. 
  Player: Two years ago we broke up a livelong friendship because you weren't brave or patient enough to stay with me.
  Player: And now you're doing exactly the same.
  Player: You can only see your problems.
-> all that happened
  Player: You are the reason why we don't have a relation anymore.
  <<set $anxiety to $anxiety + 1>>
  Player: I know you're dealing with a lot of stuff, so what?
  Player: Me too. We all are.
  Player: I may have done some bad things, like everyone, but you're the only reason why a lot of people have abandoned you.
<<jump AfterNicoBad>>
===

title: AfterMarcoComes
position: 2134,116
---
Hey! Thanks for playing ^-^
I'm so sorry, this is all we got for today.
There are still two more endings down this path, but we had stop to get to the jam's deadline.
If you still didn't get to the other endings you can try playing again!
Thank you again and have a beautiful day.
<<theEnd 5>>
===

title: NeutralEnding
position: 2130,261
---
Player: I understand that I am not going to heal the wounds that I made in our relationship just like that.
Player: And I have a lot of issues that I have to work out.
Player: You really had to put up with me today, I'm sorry.
Remie: It's ok. I invited you here.
Remie: I know what I was getting into.
Remie: But please, get help, ok?
Remie: You sound like you really need to unload some heavy stuff.
Player: You're right. You did a lot for me.
Remie: But I can't be the one to help you until you sort yourself out.
Remie: You understand that, right?
Player: Yeah... thanks.
Remie: It was nice seing you, Nico. We should do it again.
<<wait 1>>
Remie: In a while.
Remie: Take care.
<<theEnd 4>>
===