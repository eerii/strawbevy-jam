title: Start
tags: 
position: -141,-936
---
// Variables
<<declare $anxiety = 0 as number>>
<<declare $offense = 0 as number>>
<<declare $nonsense = 0 as number>>
<<declare $alcohol = 0 as number>>
<<declare $busy_work = false as bool>>
<<declare $talk_about_nico = false as bool>>
<<declare $deadname = false as bool>>
<<declare $weather = "" as string>>
<<declare $drink = "" as string>>
// Start
Remie: Hey! I-it's been a while.
Player: Yeah, thanks for coming on ___.
-> (this) rainy day // We are going to use parentheses to indicate parts that can change between questions
  <<set $weather to "rainy">>
-> (this) sunny day
  <<set $weather to "sunny">> 
// No need for other
Remie: Oh please, thank [happy]you[/happy]. I hope hearing from me wasn't a bother. 
Player: Not at all! I was really excited to see you again.
Player: Hmm... it seems like they're having ___.
-> (a) busy day
  <<set $busy_work to true>>
  <<set $anxiety to $anxiety + 1>>
  Remie: Yeah, we weren't the only ones to think to have a drink today.
  <<jump BusyDay>>
-> (a) calm day
  Remie: Yeah, thank goodness! This place can get really [anxious]crowded[/anxious] sometimes.
  <<jump WaiterComes>>
-> (a) rainy day // Same as a busy day
  <<set $busy_work to true>>
  <<set $anxiety to $anxiety + 1>>
  Remie: I mean, that's one way to put it, right?
  <<jump BusyDay>>
-> (a) sunny day // Same as a calm day
  Remie: Yeah, it feels so [happy]calm[/happy] in here, specially with all the rain pouring outside.
  <<jump WaiterComes>>
// No need for other
===

title: BusyDay
position: -665,-557
---
Remie: Marco's going to take a while...
Player: Wait, is Marco still the waiter?
Remie: Of course he is! He's the [proud]best[/proud], can't get fired. 
Player: Haha I suppose that's true.
Remie: Seems like you don't come round here often. 
Player: Well, not really. It's not ___.
-> (a) good momment | (a) sunny day // We can use |Â to say that two options lead to exactly the same result
  Remie: Oh, did something happen?
  Player: [anxious]I...[/anxious] I just don't wanna talk about that right now, sorry. 
  Remie: Don't be sorry. Is there something I can do to make you feel [happy]confortable[/happy]?
  Player: Don't worry, it's fine. I'm fine. 
  Remie: Ok...
  Remie: You know what? This table made me remember something that always cheers me up.
  Remie: I still have the [proud]picture[/proud]
  <<set $talk_about_nico to true>>
-> (the) old days
  <<set $anxiety to $anxiety + 1>>
  Remie: [anxious]W-well[/anxious], I thougth you liked the old days.
  Player: I used to, yeah... but they're in the past. 
  Remie: And you don't want to remember the past?
  Player: Is that why you invited me here? To dwell on ancient times? 
  Remie: Ahm... not really...
  Remie: Do you remember the picture?
-> other
  <<set $nonsense to $nonsense + 1>>
  Remie: I don't think I quite get what you mean.
  <<wait 1>>
  Remie: You know what I just thought, sitting on this table?
  Nico: Hmm... How much they need a renovation?
  Remie: No silly, I remembered [happy]the picture[/happy]
<<jump CrookedPicture>>
===

title: WaiterComes
position: 641,-579
---
<<marcoComes>>
Waiter: Hey guys, having ya both back is great!
Waiter: Remie I'm used to seeing, but Nico, long time no see. Ya gotta catch me up later.
Remie: Hey, Marco! [happy]You[/happy] need to tell me all about this new person you're seeing.
Waiter: Oh, just ya wait, you're in for a treat. But first, can I get ya something to drink?
Remie: Nico, are you having the same as always?
Player: I guess I'll have ___
-> (an) orange juice
  Remie: An orange juice? That's new.
  <<set $drink to "orangejuice">>
  <<jump OrangeJuice>>
-> (a) beer
  Remie: Back to the old days...
  <<set $alcohol to 1>>
  <<set $drink to "beer">>
-> (a) good moment
  Remie: So that's a beer, am I right?
  Player: You know me too well.
  <<set $alcohol to 1>>
  <<set $drink to "beer">>
-> (a) rainy day
  Remie: Well, I guess it's a beer then.
  <<set $alcohol to 1>>
  <<set $drink to "beer">>
-> other
  <<set $nonsense to $nonsense + 1>>
  Remie: You always have the weirdest drinks.
  <<set $drink to "weird">>
<<jump OtherDrink>>
===

title: OrangeJuice
position: 14,-461
---
Remie: I'm having the same! I recently developed an obsession with [happy]orange juice[/happy]. 
Waiter: Two orange juices right up! I'm defenitly sitting with both of ya as soon as I can. 
Player: Please do!
<<marcoLeaves>>
<<wait 1>> // Wait for a bit
Player: So... An obsession with orange juice? 
Remie: Yep! Somehow this drink always puts me at ease. 
Player: Oh, what ___!
-> (a) weirdo
  <<set $anxiety to $anxiety + 1>>
  <<set $offense to $offense + 1>>
  Remie: Jeez... no need to be so harsh.
-> (a) coincidence
  <<set $anxiety to $anxiety - 1>>
  Remie: It's such a tasty drink, everyone is missing out.
-> other
  <<set $nonsense to $nonsense + 1>>
  Remie: You're not making much sense, Nico.
  Player: Sorry, I'm a bit out of it today.
  Remie: Don't apologize, please. Is there something I can do to make you feel more [happy]comfortable[/happy]?
  Player: It's nothing. I'm [anxious]fine[/anxious].
  <<set $talk_about_nico to true>>
  Remie: Ok...
  Player: It's cool. The orange juice I mean.
Player: If you like it so much you should get a poster of it.
Remmie: I would if I had some free space in my room.
Remmie: I still have the picture, by the way.
<<jump CrookedPicture>>
===

title: OtherDrink
position: 440,-301
---
Remie: I'm having an orange juice Marco, thanks. 
Waiter: So, an orange juice, [select $drink beer="a beer" weird="an... imma bring you a glass of water"/], and some tea I gotta spill.
Waiter: Be right back!
<<marcoLeaves>>
<<wait 1>>
Remie: So, we really are on the same bar as two years ago. 
Player: Well, we're even on the same table.
Remi: [happy]Of course![/happy] I love this table. Do you remember our game nights? 
Player: Yes I do! And ___ 
-> (the) hour long arguments
  <<set $anxiety to $anxiety + 1>>
  Remie: Those... I guess we can get really stubobrn.
-> (the) band performances
  Remie: Shut up! Those really were so funny.
-> (the) good moment(s)
  Remie: Yeah, we got plenty of those, didn't we?
  <<set $anxiety to $anxiety - 1>>
-> (the) busy day(s) | (the) calm day(s) | (the) rainy day(s) | (the) sunny day(s)
  Remie: Those were the days, huh?
-> other
  <<set $nonsense to $nonsense + 1>>
  Remie: I'm having a hard time remembering that.
Player: It seems like ___ ago
-> (a) long time
  <<set $anxiety to $anxiety + 1>>
  Remie: Does it? For me is an everyday thing, after all, I see the picture every morning.
-> (a) little while
  <<set $anxiety to $anxiety - 1>>
  Remie: Does it? For me is an everyday thing, after all, I see the picture every morning.
-> hour long arguments
  <<set $anxiety to $anxiety + 1>>
  Remie: I guess you can measure time in arguments. We didn't have a shortage of them.
  Remie: Every time I look at the picture I can't help but remembering everything.
-> other
  <<set $nonsense to $nonsense + 1>>
  Remie: You are always so weird.
  Remie: Like in the picture.
<<jump CrookedPicture>>
===

title: CrookedPicture
position: 181,-4
---
<<discard>> // Make the player discard their cards when entering a new scene
Player: Wait, the crooked picture?
Remie: Yep, we look so goofy.
<<if $talk_about_nico>>
    <<jump TalkAboutNico>>
<<else>>
    <<jump TalkAboutRemie>>
<<endif>>
===

title: TalkAboutNico
position: -489,277
---
Player: You keept it...
Player: That's kind of ___ to me.
-> (an) important thing
  <<set $anxiety to $anxiety - 1>>
  Remie: I am so relieved you said that.
  Remie: Sometimes when I think about my life before coming out it feels unreal.
  Remie: Like I was watching a movie character, not myself.
  Remie: This picture... it reminds me that there can be light in unexpected places.
  Remie: Even if it's crooked haha.
  Remie: I hope it can do the same for you.
-> (a) trigger
  <<set $anxiety to $anxiety + 1>>
  Remie: Oh...
  Remie: Can I ask you why?
<<jump NicoDepressed>>
===

title: NicoDepressed
position: -445,675
---
Player: You know?
Player: In these two years I achieved ___ I wanted.
-> everything | (every) important thing
  Remie: Oh, did you finally get your doctorate?
  Player: Yes.
  Remie: Congrats! It must have been a lot of work.
  Remie: What about Marie? And your son?
  Player: They couldn't be better.
-> nothing
  <<set $anxiety to $anxiety - 1>>
  Player: I never feelt so bad, and it gets worse every day.
  Remie: Nico... did something happen with Marie?
  Player: No, we are still together. 
  Remie: Academic troubles?
  Player: Not really. 
  Player: I'm a doctor now.
  Remie: Well, congrats! You finally got it.
-> (any) trigger
  <<set $nonsense to $nonsense + 1>>
  Remie: What do you mean?
  Player: I don't know where my head is today.
  Player: But to be honest, I don't know where it has been lately.
  Remie: Nico, are you ok?
  Player: I should be. I have a loving family, I got my doctorate...
  Remie: You did? Congratulations!
Remie: Shouldn't you be happy, then? 
<<jump NicoPerfect>>
===

title: NicoPerfect
position: -397,1043
---
Player: Well, I thought so.
Player: I thought that if I got to the finish line, everything would be fine.
Player: But every time I got closer, there was something else.
Remie: Like when you got that paper published, and we got together to celebrate.
Player: Oh... you remember that time.
Remie: Of course. We were all so happy, but you didn't really enjoy it.
Remie: And then you spent the next two weeks in your office, working on your thesis.
Player: Yeah...
Player: The things that are supposed to make me happy are just tiny speckles.
Player: But I really thought that after I finish them all, I could finally be happy.
Remie: You can't really treat life like a to-do list.
Player: I know. Or at least I thought I knew.
Player: Now that I have all I wanted, it feels like ___.
-> (a) waste of time | nothing | (not) (an) important thing
-> (a) jail
  Remie: I think I can relate with that haha.
-> everything
  Remie: Everything?
  Player: Yeah, like even everything is not enough.
-> (pulling) (the) trigger
  <<set $anxiety to $anxiety + 2>>
  Remie: Nico, are you ok?
  Player: I'm fine. I didn't mean to say that.
<<wait 1>>
Player: I may be crooked too [anxious]haha[/anxious].
Remie: Well, that makes two of us.
Player: It's not like I don't love my wife anymore.
Player: Or that suddenly I hate mathematics.
<<if $anxiety < 4>>
  Remie: I would end you if you did, after all the hours I had to listen you rumble about...
  Remie: What was it called, again? Motivations theory?
  Player: Haha, yeah. I may have talked too much about [proud]motives[/proud] theory.
  Remie: That's the one!
  Remie: Also sorry, hope you know I was just joking.
  Remie: I liked listening to you talk about it, even if I didn't understand a thing.
  Remie: Anyways, sorry for getting sidetracked.
  Player: Don't apologize. It's just that, nothing is [anxiety]terrible[/anxiety]...
<<endif>>
Player: But I don't do sports anymore.
Player: I don't see my friends.
Player: And the motivation that I had when I was studying,
Player: now I got what I wanted,
Player: is no more.
Remie: I think I get that.
Remie: Like everything is fine with your life but there's someone on the backseat that makes you unable to enjoy it.
Player: ___.
-> !Exactly
  <<set $anxiety to $anxiety - 1>>
  <<jump Exactly>>
-> !Not really
  <<jump NotReally>>
===

title: Exactly
position: -421,1579
---
<<set $anxiety to $anxiety - 1>>
Remie: I felt like that before I knew I was enby.
Remie: Of course I'm not trying to say you are in the middle of a [scary]gender crisis[/scary].
Remie: But I know the feeling of not being able to recognize yourself in your life.
Remie: You shouldn't have guilt, it's not as if you're doing anything bad. 
Remie: Sometimes things just don't fit in together.
Player: I suppose it makes sense. 
Player: Thanks.
Player: It's just... my life is like ___.
-> nothing | (a) waste time | (a) jail | (not) (an) important thing
  <<set $anxiety to $anxiety - 1>>
  Player: Sometimes I don't really feel like living it. 
  Remie: Hey, please, don't say that. 
  Remie: I know it can be overwhelming sometimes. 
  Remie: And you have a lot of pressure on you.
  Remie: You did what you desired the most.
  Remie: But that doesn't mean that you [intensely]have to[/intensely] to be right. 
  Remie: Everyone needs help sometimes, that includes you. 
  Remie: All of us have to deal with the conflict from time to time. 
-> everything
  Player: Everything to me. 
  Player: And the mere idea of not enjoying it is disgraceful. 
  Remie: I know it can be overwhelming sometimes.
  Remie: And I know that you may be thinking that the hole you feel you have in your life is wrong.
  Remie: But it's not that.
  Remie: Not everything has to always fit together.
  Remie: It doesn't mean that you have to get away from all of it.
  Remie: Perhaps you may need a different approach.
  Player: Well, I got away you and now everything is worse. 
  Remie: Exactly, kicking me out is the most [proudly]terrible idea[/proudly] anyone could have.
  Remie: You can have to ask my parents about it.
  Player: Wait, wha...?
-> (grabbing) (the) trigger
  <<set $anxiety to $anxiety - 1>>
  Player: Ready to give up.
  Remie: Nico, please, don't say that.
  Remie: I know it can be overwhelming sometimes.
  Remie: When you are drowning, you can't think about anything else.
  Remie: But please, don't give up.
  Remie: You might think that it will never get better, but it will.
  Remie: I know it's hard to believe, but it will.
  Player: I don't know.
  Remie: You don't need to. You can figure it out on the way.
  Remie: You can have a lot of pieces that don't fit together and still be happy.
  Player: Thanks. It feels good to talk to you.
  Remie: I really missed this, you know?
<<jump AfterNicoGood>>
===

title: NotReally
position: 1226,-709
---
<<set $anxiety to $anxiety + 1>>
Remie: So, what is it then? 
Remie: You know, this thing you're telling me really feels like my life before knowing I'm enby
Remie: This uncunny feeling of not selfrecognition.
Remie: And I think is completly normal even in a cis person.
Remie: You reached the goal, so, what now?
Player: Well, I don't think we are the same.
Player: You are ___ and I'm...
-> (a) waste of time | nothing
  <<set $anxiety to $anxiety + 2>>
  Player: I'm here just for pity.
  Player: I thought you may have changed a bit. 
  Player: Perhaps we could have a nice chat, that's all. 
  Player: But I didn't really want to come. 
  Player: This recognition you talk about is a no sense.
  Player: I haven't thought on you in the last two years.  
  <<jump AfterNicoBad>>
-> (a) jail | (a) trigger
  <<set $anxiety to $anxiety + 2>>
  Player: I'm just overpassed with you.
  Player: I don't feel right with you.
  Player: Being here remember me why we are not friends anymore. 
  Player: My life has nothing to do with this nonsense of you.
  <<jump AfterNicoBad>>
-> everything
  Player: I'm just envious of you.
  Player: In a good way. Well. I hope so. 
  Player: I didn't really get to understand you, but I realized I didn't need to. 
  Player: I admire you just because, even if I know we are not the same, I have to recognize you something:
  Player: know how to be ok with your life from a base, not depending on some goals, 
  Player: but having the endurance to take actions in virtue of what it's ok with you regardless of some made-up end.
  <<jump AfterNicoGood>>
-> (an) important thing
  Player: I'm silly. Uncapable to recognize it. 
  Player: It's not like I had the faith of solve my problems by seeing you today. 
  Player: Or you acepting me like that. 
  Player: I know you've dealing with a lot and, honestly, I don't really understand you. 
  Player: I didn't come here to be friends again,
  Player: but to meet again with something that have scared me for the last years. 
  Player: So, no, we are not the same: I find you so different that I couldn't even mantain a relation with you,
  Player: but now, over time,
  Payer: I learnt that this distance is important, an so you should be.
  <<jump AfterNicoGood>>
===

title: AfterNicoGood
position: 1953,-410
---
<<discard>>
*Remie interrupts the conversation when they see Marco coming*
Remie: Oh, look who's coming
<<if $busy_work>>
  Player: Marco! I can't belive you still work here. 
  Waiter: Well, I'm simply the best, the surprise here is you coming back, Nico. And coming with Remie!
  Player: Well, yes, I didn't come in a while.
  Waiter: Well that's true if a while equals two years.
  Waiter: Remie, I know you understand me, we really gotta explain maths to this lad. 
  Remie: Oh yes we should.
<<endif>>
Remie: Have you got some time to sit with us for a bit, Marco?
Waiter: I do! But don't ya want a drink?
Player: We can have it later, first the chat!
Waiter: What have tou being doing the last two years, Nico? 
Player: Well, you now just ___
-> work
  <<jump NicoWork>>
-> a family
  <<jump NicoFamily>>
===
title: NicoWork
position: 1815,110
---
Waiter: Ah yes, that [bored]mathematician[bored] stuff.
Remie: Yeah, Marco, careful, he can really operate us haha.
Player: Thanks, lads, I missed your special support. 
Waiter: Sure ya did! So, still a doctoral student? 
Player: Well, not really.
Player: I finished the thesis. 
Waiter: WOW! That's great, congrats, Mr. Doctor! I don't know what are ya drinking but it's on the house. 
Waiter: You may be really in a sweet life momment so.
Player: Well, kinda
===

title: AfterNicoBad
position: 1637,-981
---
Waiter: Hey, lads, it's great to have ya both ba...
Remie: You know what?
Remie: I don't have to endure this.
Remie: And I'm not doing it.
Remie: Thakfuly I have a good job and an amazing circle.
Remie: I didn't even want us to be friends again. 
Remie: I just needed to look at the past. 
Remie: Now I firmly say I'm enby.
Remie: Regardless you like it or not. 
Remie: I don't need you to afirme me. 
Remie: Goodbye 
<<Wait 1>>
Waiter: What's happening here?
Player: Oh hey Marco.
Player: It's nothing. 
Player: You know he can be ___
-> a drama queen. 
-> an idiot. 
Waiter: Well, not, I don't know it. 
Waiter: They is a very good friend and someone you can relay on.
Waiter: It seems like Remie's not the problem here. 
Waiter: You know English, you can say "they". 
Player: Oh come on. 
Payer: He just want me to come here to show off in front of me, it was just a revenge. 
Waiter: No, you expected this to be a battle. 
Waiter: You know what? I don't want you here.
Waiter: Get out of the bar.
Waiter: I don't wanna see you anymore.
===

title: TalkAboutRemie
position: 584,346
---
Player: So you still have it ___.
-> (in) the same place
  <<set $anxiety to $anxiety + 1>>
  Remie: Not really. 
  <<wait 1>>
  Remie: My parents kicked me out of home when I came out.
  Remie: I'm sharing an apartment downtown.
  Remie: The picture is one of the few things I got from my house after... well, [anxious]that[/anxious].
  Remie: But hey, it's still crooked.
  Player: I'm sorry. Your parents are such jerks.
  Remie: Well, it is what it is.
  Remie: No point beating a dead horse. 
  <<if $busy_work>>
    <<jump BusyDayMarcoDrinks>>
  <<else>>
    <<jump AfterMarcoComes>>
  <<endif>>
-> (after) all that happened
  Remie: Did you want me to throw it away?
  <<wait 1>>
  Remie: Nevermind. Don't answer that.
  Remie: I was close, and I was tempted to leave it behind when I moved out.
  Remie: Now I have this nice job, and a [proud]supportive[/proud] group of friends.
  Remie: In this time when we didn't see each other I made myself a life, one worth living.
  Remie: That picture reminds me:
  Remie: "You can look at the past".
  Remie: That's why I invited you here today.
  <<jump LookDeadname>>
===

title: LookDeadname
position: 838,791
---
Player: Look, ___, about that...
-> !Remie // ! is used to specify that only these options should be available
  Remie: I know.
  Remie: I don't expect everything to be the same as before.
  Remie: I know you're not the same person you were when we were friends.
  Remie: I certainly am not.
  Remie: But I'm glad you came.
  Player: I'm glad too.
-> !Mario
  <<set $anxiety to $anxiety + 2>>
  <<set $offense to $offense + 2>>
  <<set $deadname to true>>
<<if $busy_work>>
  <<jump BusyDayMarcoDrinks>>
<<elseif $deadname>>
  <<jump AfterDeadname>>
<<else>>
  <<jump AfterMarcoComes>>
<<endif>>
===

title: BusyDayMarcoDrinks
position: 1754,790
---
<<marcoComes>>
Waiter: Hey guys! I didn't think I would see ya both together again!
<<if $deadname>>
  Waiter: Sorry for not coming ear...
  Remie: Don't worry, Marco. Can you please leave us for another moment?
  Waiter: Of course, is everything ok?
  Remie: Yes, I just need to talk with my [sarcastic]friend[/sarcastic].
  Waiter: Sure, I'll be back in a few.
  Waiter: Call me if you need... anything.
  <<marcoLeaves>>
  <<wait 1>>
  <<jump AfterDeadname>>
<<else>>
  Waiter: I whish I had less work so I could chat with ya for a few.
  Remie: Don't worry, Marco! You can join [emotional]us[/emotional] later.
  Waiter: For now, what can I get ya?
  Remie: I'm having an orange juice please.
  Player: I'll have ___.
  -> (the) same
    Remie: You also like orange juice?
    Player: It's not my favorite, but I don't want to drink alcohol.
  -> (a) beer
    <<set $alcohol to 1>>
    Remie: It seems like the picture is not the only thing that stays the same.
  Waiter: Will be with ya as soon as I can.
  Player: It's really nice to see you, Marco.
  <<marcoLeaves>>
  <<wait 1>>
  <<jump AfterMarcoComes>>
<<endif>>
===

title: AfterDeadname
position: 1276,390
---
Remie: You know what, Nico?
Remie: I really thought we could talk again, but I guess I was wrong.
Player: I didn't mean to ___ you.
-> offend 
  Player: it's just a name. 
  Remie: You know is more than that. 
  Remie: Calling someone by it's deadname [emphasis]is[/emphasis] offensive, no matter the intntions. 
  Remie: Why do you think we didn't see each other in two years? Exactly because you couldn't treat me as you should.
-> give you hope
  Player: by acepting the invitation
  <<set $anxiety to $anxiety + 2>>
  Remie: Give me hope? Hope for what?
  Remie: Look, I needed to see you to ensure me, not to desparetly be yout friend again.
===

title: AfterMarcoComes
position: 2070,354
---

===